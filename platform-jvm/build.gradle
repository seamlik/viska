apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'

version = PROJECT_VERSION
sourceCompatibility = 8

dependencies {
  api "io.grpc:grpc-stub:${VERSIONS.grpcJava}"
  api 'com.google.protobuf:protobuf-java:3.13.0'
  api 'io.grpc:grpc-kotlin-stub:0.2.0'
  api 'javax.inject:javax.inject:1'
  implementation "io.grpc:grpc-protobuf:${VERSIONS.grpcJava}"
  implementation 'commons-codec:commons-codec:1.15'
  implementation 'javax.annotation:javax.annotation-api:1.3.2'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.0-M1'
}

sourceSets {
  main {
    proto {
      srcDir '../grpc'
    }
  }
}

compileKotlin {
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.13.0'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${VERSIONS.grpcJava}"
    }
    grpckt {
      artifact = 'io.grpc:protoc-gen-grpc-kotlin:0.2.0:jdk7@jar'
    }
  }
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {}
      }
      task.plugins {
        grpc {}
        grpckt {}
      }
    }
  }
}