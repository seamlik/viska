apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.diffplug.spotless'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

repositories {
  google()
  jcenter()
  mavenLocal()
}

android {
  compileSdkVersion 30
  compileOptions {
    sourceCompatibility 8
    targetCompatibility sourceCompatibility
  }
  buildFeatures {
    compose true
  }
  composeOptions {
    kotlinCompilerExtensionVersion VERSIONS.compose
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
  defaultConfig {
    applicationId 'viska.android'
    minSdkVersion 28
    targetSdkVersion 30
    versionName '0'
    versionCode 1
  }
  splits {
    abi {
      enable true
      reset()
      include 'arm64-v8a', 'armeabi-v7a', 'x86_64'
    }
  }
  sourceSets {
    main {
      java {
        srcDir '../target/riko/jni'
      }
      proto {
        srcDir '../grpc'
      }
    }
  }
  packagingOptions {
    exclude '/META-INF/INDEX.LIST'
    merge '/META-INF/io.netty.versions.properties'
  }
}

spotless {
  kotlin {
    ktfmt('0.18')
    target '**/*.kt'
    targetExclude '**/viska/android/composable.kt'
  }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.13.0'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${VERSIONS.grpcJava}"
    }
    grpckt {
      artifact = 'io.grpc:protoc-gen-grpc-kotlin:0.2.0:jdk7@jar'
    }
  }
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {}
      }
      task.plugins {
        grpc {}
        grpckt {}
      }
    }
  }
}

configurations.all {
  exclude group: 'javax.annotation', module: 'jsr250-api'
}

dependencies {
  implementation "androidx.compose.foundation:foundation-layout:${VERSIONS.compose}"
  implementation "androidx.compose.material:material-icons-extended:${VERSIONS.compose}"
  implementation "androidx.compose.material:material:${VERSIONS.compose}"
  implementation "androidx.compose.runtime:runtime-livedata:${VERSIONS.compose}"
  implementation "androidx.compose.ui:ui:${VERSIONS.compose}"
  implementation 'androidx.core:core-ktx:1.3.2'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
  implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
  implementation 'androidx.preference:preference-ktx:1.1.1'
  implementation "androidx.ui:ui-tooling:${VERSIONS.compose}"
  implementation 'com.couchbase.lite:couchbase-lite-android:2.8.0'
  implementation 'commons-codec:commons-codec:1.15'
  implementation 'com.google.android.material:material:1.3.0-alpha03'
  implementation 'com.google.dagger:hilt-android:2.29.1-alpha'
  implementation 'com.google.protobuf:protobuf-java:3.13.0'
  implementation 'io.grpc:grpc-kotlin-stub:0.2.0'
  implementation "io.grpc:grpc-netty:${VERSIONS.grpcJava}"
  implementation "io.grpc:grpc-protobuf:${VERSIONS.grpcJava}"
  implementation "io.grpc:grpc-stub:${VERSIONS.grpcJava}"
  implementation 'javax.annotation:javax.annotation-api:1.3.2'
  implementation 'riko:riko-runtime-jni:0.0.1'
  kapt "com.google.dagger:hilt-android-compiler:${VERSIONS.hilt}"
  testImplementation 'junit:junit:4.13'
}