apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.diffplug.spotless'
apply plugin: 'com.google.protobuf'

repositories {
  google()
  jcenter()
  mavenLocal()
}

android {
  compileSdkVersion 30
  compileOptions {
    sourceCompatibility 8
    targetCompatibility sourceCompatibility
  }
  buildFeatures {
    compose true
  }
  composeOptions {
    kotlinCompilerExtensionVersion '1.0.0-alpha02'
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
  defaultConfig {
    applicationId 'viska.android'
    minSdkVersion 28
    targetSdkVersion 30
    versionName '0'
    versionCode 1
  }
  splits {
    abi {
      enable true
      reset()
      include 'arm64-v8a', 'x86_64'
    }
  }
  sourceSets {
    main {
      java {
        srcDir '../target/riko/jni'
      }
      proto {
        srcDir '../grpc'
      }
    }
  }
  packagingOptions {
    exclude '/META-INF/INDEX.LIST'
    merge '/META-INF/io.netty.versions.properties'
  }
}

spotless {
  java {
    googleJavaFormat('1.9')
    target '**/*.java'
  }
  kotlin {
    ktfmt('0.18')
    target '**/*.kt'
  }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.13.0'
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.32.1'
    }
    grpckt {
      artifact = 'io.grpc:protoc-gen-grpc-kotlin:0.1.5'
    }
  }
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {}
      }
      task.plugins {
        grpc {}
        grpckt {}
      }
    }
  }
}

dependencies {
  implementation 'androidx.compose.foundation:foundation-layout:1.0.0-alpha02'
  implementation 'androidx.compose.material:material:1.0.0-alpha02'
  implementation 'androidx.compose.runtime:runtime-livedata:1.0.0-alpha02'
  implementation 'androidx.compose.ui:ui:1.0.0-alpha02'
  implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
  implementation 'androidx.core:core-ktx:1.3.1'
  implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
  implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
  implementation 'androidx.preference:preference-ktx:1.1.1'
  implementation 'androidx.ui:ui-tooling:1.0.0-alpha02'
  implementation 'com.couchbase.lite:couchbase-lite-android:2.7.1'
  implementation 'com.google.android.material:material:1.3.0-alpha02'
  implementation 'com.google.android:flexbox:2.0.1'
  implementation 'com.google.protobuf:protobuf-java:3.13.0'
  implementation 'io.grpc:grpc-kotlin-stub:0.1.5'
  implementation 'io.grpc:grpc-netty:1.32.1'
  implementation 'io.grpc:grpc-protobuf:1.32.1'
  implementation 'io.grpc:grpc-stub:1.32.1'
  implementation 'javax.annotation:javax.annotation-api:1.3.2'
  implementation 'riko:riko-runtime-jni:0.0.1'
  testImplementation 'junit:junit:4.13'
}